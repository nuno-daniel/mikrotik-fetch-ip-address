:local downloadComplete false

# Download the file
/tool fetch url="https://www.cloudflare.com/ips-v4" mode=https dst-path=cloudflare-ips-v4

# Wait for the file to be downloaded
:do {
    :delay 1s
} while=([/file get [/file find name="cloudflare-ips-v4"] size] = 0)

# Check if the file was downloaded successfully
:if ([/file get [/file find name="cloudflare-ips-v4"] size] > 0) do={
    :log info "Cloudflare IPv4 address list file downloaded successfully."
    :set downloadComplete true
} else={
    :log error "Cloudflare IPv4 address list file download failed."
}

# Proceed with script execution if download was successful
:if ($downloadComplete) do={
    # Remove existing addresses from the current Address list
    /ip firewall address-list remove [/ip firewall address-list find list=Cloudflare-IPv4]
    
    :local content [/file get [/file find name="cloudflare-ips-v4"] contents]
    :local contentLen [:len $content]
    
    :local lineEnd 0
    :local line ""
    :local lastEnd 0
    
    :do {
        :set lineEnd [:find $content "\n" $lastEnd]
        :set line [:pick $content $lastEnd $lineEnd]
        :set lastEnd ($lineEnd + 1)
        
        # If the line doesn't start with a hash then process and add to the list
        :if ([:pick $line 0 1] != "#") do={
            :local entry [:pick $line 0 $lineEnd]
            :if ([:len $entry] > 0 && $entry != "1") do={
                /ip firewall address-list add list=Cloudflare-IPv4 address=$entry
            }
        }
    } while=($lineEnd < $contentLen)
    
    :log info "Cloudflare IPv4 addresses added to address list."
} else={
    :log error "Cloudflare IPv4 addresses could not be added to address list due to download failure."
}
