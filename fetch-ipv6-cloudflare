:local downloadComplete false

# Download the IPv6 file
/tool fetch url="https://www.cloudflare.com/ips-v6" mode=https dst-path=cloudflare-ips-v6

# Wait for the file to be downloaded
:do {
    :delay 5s
} while=([/file get [/file find name="cloudflare-ips-v6"] size] = 0)

# Check if the file was downloaded successfully
:if ([/file get [/file find name="cloudflare-ips-v6"] size] > 0) do={
    :log info "Cloudflare IPv6 address list file downloaded successfully."
    :set downloadComplete true
} else={
    :log error "Cloudflare IPv6 address list file download failed."
}

# Proceed with script execution if download was successful
:if ($downloadComplete) do={
    # Remove existing addresses from the current IPv6 address list
    /ipv6 firewall address-list remove [/ipv6 firewall address-list find list=Cloudflare-IPv6]
    
    :local content [/file get [/file find name="cloudflare-ips-v6"] contents]
    :local contentLen [:len $content]
    
    :local lineEnd 0
    :local line ""
    :local lastEnd 0
    
    :do {
        :set lineEnd [:find $content "\n" $lastEnd]
        :set line [:pick $content $lastEnd $lineEnd]
        :set lastEnd ($lineEnd + 1)
        
        # If the line doesn't start with a hash then process and add to the list
        :if ([:pick $line 0 1] != "#") do={
            :local entry [:pick $line 0 $lineEnd]
            :if ([:len $entry] > 0 && $entry != "2") do={
                /ipv6 firewall address-list add list=Cloudflare-IPv6 address=$entry
            }
        }
    } while=($lineEnd < $contentLen)
    
    :log info "Cloudflare IPv6 addresses added to address list."
} else={
    :log error "Cloudflare IPv6 addresses could not be added to address list due to download failure."
}
